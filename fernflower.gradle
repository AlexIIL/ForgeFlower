apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

version = '1.0.301-SNAPSHOT' // OurVersion.FernFlowerCommitNumber-SNAPSHOT
group = 'net.minecraftforge' //put it under us for now, because this is a unofficial mirrior
archivesBaseName = 'forgeflower'
 
sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}
repositories {
    mavenCentral()
}
configurations {
    deployerJars
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.assertj:assertj-core:3.6.1'
}

compileJava.options.encoding = 'utf-8'
compileJava.options.deprecation = true

jar.manifest {
    attributes 'Main-Class' : 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        
        if (project.hasProperty('forgeMavenPass')) {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: "forge", password: project.getProperty('forgeMavenPass'))
            }
        }
        else
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Fernflower from https://github.com/JetBrains/intellij-community as standalone library, With Forges Modifications'
                url 'https://github.com/MinecraftForge/ForgeFlower'

                scm {
                    url 'https://github.com/MinecraftForge/ForgeFlower'
                    connection 'scm:git:git://github.com/MinecraftForge/ForgeFlower.git'
                    developerConnection 'scm:git:git@github.com:MinecraftForge/ForgeFlower.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/MinecraftForge/ForgeFlower/issues'
                }

                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

